// SPDX-License-Identifier: Apache-2.0
pragma solidity 0.8.15;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import "./interfaces/IChannelHandler.sol";

/**
 * @dev The contract was analyzed using Slither static analysis framework. All recommendations have been taken
 * into account and some detectors have been disabled at developers' discretion using `slither-disable-next-line`.
 */
contract ChannelHandler is Ownable, ReentrancyGuard, IChannelHandler {
    using ECDSA for bytes32;

    // the last submitted message nonce
    uint256 public message_nonce;
    // the last submitted batch nonce
    uint256 public batch_nonce;
    //number of authorized peers
    uint256 public peersCount;
    //indicates whether address is an authorized peer
    mapping(address => bool) public isPeer;
    //indicates whether address is an authorized app
    mapping(address => bool) public isApp;
    //signers of the bath by batch nonce
    mapping(uint256 => mapping(address => bool)) public signedPeers;

    uint256 public constant GAS_BUFFER = 60000;
    bytes2 public constant ADD_PEER_CALL = 0x0101;
    bytes2 public constant REMOVE_PEER_CALL = 0x0102;

    constructor() {}

    // Once-off post-construction call to set initial configuration.
    function initialize(address[] calldata initialPeers) external onlyOwner {
        for (uint256 i; i < initialPeers.length; i++) {
            addPeer(initialPeers[i]);
        }
        isApp[address(this)] = true;
        // drop admin privileges
        renounceOwnership();
    }

    function submit(
        Batch calldata batch,
        uint8[] calldata v,
        bytes32[] calldata r,
        bytes32[] calldata s
    ) external nonReentrant {
        uint256 begin_gas_left = gasleft();
        if (peersCount < 1) revert InvalidPeersCount();
        if (batch.messages.length >= 256) revert InvalidMessagesLength();
        batch_nonce = batch_nonce + 1;
        // Check batch nonce is correct for replay protection
        if (batch.nonce != batch_nonce) revert InvalidNonce();

        // Since we verify that the batch was generated by substrate, we can safely
        // assume that all fields are correct (for example, `max_gas` are
        // appropriate, and `total_max_gas` = `sum(max_gas)`)

        bytes32 commitment = keccak256(abi.encode(batch));
        bytes32 digestHash = keccak256(
            abi.encode(block.chainid, commitment)
        );

        if (!verifySignatures(digestHash, v, r, s)) revert InvalidSignature();
        // Require there is enough gas to execute all messages
        if (gasleft() < batch.total_max_gas + GAS_BUFFER)
            revert InsufficientGas();

        uint256 results = processMessages(batch.messages);

        uint256 gas_used = begin_gas_left - gasleft();

        emit BatchDispatched(
            batch_nonce,
            msg.sender,
            results,
            batch.messages.length,
            gas_used,
            block.basefee
        );
    }

    function submitMessage(bytes calldata payload) external {
        if (!isApp[msg.sender]) revert Unregistered();
        message_nonce = message_nonce + 1;
        emit MessageDispatched(msg.sender, message_nonce, payload);
    }

    function addPeerByPeer(address newPeerAddress) external returns (bool) {
        if (msg.sender != address(this)) revert InvalidCaller();

        //TODO set up call signature
        bytes memory call = abi.encodePacked(
            ADD_PEER_CALL,
            newPeerAddress
        );

        addPeer(newPeerAddress);
        emit ChangePeers(newPeerAddress, false);
        this.submitMessage(call);
        return true;
    }

    function registerApp(address newApp) external returns (bool) {
        if (msg.sender != address(this)) revert InvalidCaller();
        if (!isApp[newApp]) revert AlreadyRegistered();
        isApp[newApp] = true;
        return true;
    }

    function removeApp(address app) external returns (bool) {
        if (msg.sender != address(this)) revert InvalidCaller();
        if (!isApp[app]) revert Unregistered();
        isApp[app] = false;
        return true;
    }

    function removePeerByPeer(address peerAddress) external returns (bool) {
        if (msg.sender != address(this)) revert InvalidCaller();

        //TODO set up call signature
        bytes memory call = abi.encodePacked(
            REMOVE_PEER_CALL,
            peerAddress
        );

        removePeer(peerAddress);
        emit ChangePeers(peerAddress, true);
        this.submitMessage(call);
        return true;
    }

    /**
     * Adds new peer to the list of signature verifiers.
     * Private function
     * @param newAddress address of new peer
     */
    function addPeer(address newAddress) private {
        if (isPeer[newAddress]) revert AlreadyRegistered();
        isPeer[newAddress] = true;
        ++peersCount;
    }

    /**
     * Removes a peer from the list of signature verifiers.
     * Private function
     * @param peerAddress address of the peer
     */
    function removePeer(address peerAddress) private {
        if (!isPeer[peerAddress]) revert Unregistered();
        isPeer[peerAddress] = false;
        if(--peersCount < 1) revert LastPeer();
    }

    /**
     * Checks given addresses for duplicates and if they are peers signatures
     * Batch nonce should be unique
     * @param hash signed data
     * @param v v-component of signature from hash
     * @param r r-component of signature from hash
     * @param s s-component of signature from hash
     * @return true if all given addresses are correct or false otherwise
     */
    function verifySignatures(
        bytes32 hash,
        uint8[] memory v,
        bytes32[] memory r,
        bytes32[] memory s
    ) private returns (bool) {
        uint256 signatureCount = v.length;
        if (signatureCount != r.length || signatureCount != s.length)
            revert SigParamsLengthMismatch(signatureCount, r.length, s.length);
        uint256 needSigs = peersCount - (peersCount - 1) / 3;
        if (signatureCount < needSigs) revert SignaturesNotEnough();

        uint256 count;
        address[] memory recoveredAddresses = new address[](signatureCount);
        for (uint256 i; i < signatureCount; ++i) {
            address recoveredAddress = hash.toEthSignedMessageHash().recover(
                v[i],
                r[i],
                s[i]
            );

            // not a peer address or not unique
            if (
                isPeer[recoveredAddress] != true ||
                signedPeers[batch_nonce][recoveredAddress] == true
            ) {
                continue;
            }
            recoveredAddresses[count] = recoveredAddress;
            unchecked {
                count = count + 1;
            }
            signedPeers[batch_nonce][recoveredAddress] = true;
        }

        return count >= needSigs;
    }

    // - result - message results bitmap, up to 256 messages
    function processMessages(
        Message[] calldata messages
    ) private returns (uint256 results) {
        for (uint256 i = 0; i < messages.length; ++i) {
            // Deliver the message to the target
            // Delivery will have fixed maximum gas allowed for the target app
            // slither-disable-next-line low-level-calls
            bool success;
            if (isApp[messages[i].target]) {
                (success, ) = messages[i].target.call{
                    value: 0,
                    gas: messages[i].max_gas
                }(messages[i].payload);
            } 
            results |= uint256(success ? 1 : 0) << i;
        }
        return results;
    }
}
